buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'antlr'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
}

group 'com.taskbase.bitmark'
archivesBaseName = 'bitmark-parser'
version '1.0'

apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "build/generated-src/antlr/main"]
        }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7.2"
    compile 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.2'
}

compileKotlin {
    dependsOn(generateGrammarSource)
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    dependsOn(generateGrammarSource)
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

generateGrammarSource {
    //  arguments = ['-Dlanguage=JavaScript']
    //  outputDirectory = file('build/generated-src/main/javascript')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

/*
 * Follow these instructions for deploying:
 * http://www.albertgao.xyz/2018/01/18/how-to-publish-artifact-to-maven-central-via-gradle/
 *
 * Create a gradle.properties file with the following entries:
 *
 * nexusUsername=[nexus username]
 * nexusPassword=[nexus password]
 * signing.keyId=[signing key id]
 * signing.password=[signing password]
 * signing.secretKeyRingFile=/path/to/sonatype-secret-keys.gpg
 *
 */
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                name 'Bitmark-Parser'
                packaging 'jar'
                description 'Bitmark Parser'
                url 'https://github.com/getMoreBrain/bitmark-parser'

                scm {
                    connection 'scm:git:https://github.com/getMoreBrain/bitmark-parser'
                    developerConnection 'scm:git:https://github.com/getMoreBrain/bitmark-parser'
                    url 'scm:git:https://github.com/getMoreBrain/bitmark-parser'
                }

                developers {
                    developer {
                        id 'jjoller'
                        name 'Jost Joller'
                        email 'jost@taskbase.com'
                    }
                }
            }
        }
    }
}